* Diagonal BEKK -----------------------------------------------------------------------------
end(reset)
OPEN DATA "C:\Users\Jimmy\Desktop\m-pfemrk6508.txt"
DATA(FORMAT=PRN,NOLABELS,ORG=COLUMNS,TOP=2,LEFT=2) 1 528 pfe mrk
garch(p=1,q=1,mv=dbekk,hmatrices=hh,rvector=rr,piters=20,pmethod=simplex,iters=200) / pfe mrk
* Diagonal BEKK -----------------------------------------------------------------------------
end(reset)
OPEN DATA "C:\Users\Jimmy\Desktop\m-pfemrk6508.txt"
DATA(FORMAT=PRN,NOLABELS,ORG=COLUMNS,TOP=2,LEFT=2) 1 528 pfe mrk
stats(noprint) pfe
set h11 = %variance
stats(noprint) mrk
set h22 = %variance
cmom(corr)
# pfe mrk
set h12 = %cmom(1,2)
nonlin p1 p2 c11 c12 c22 a11 a22 b11 b22
frml a1t  = pfe - p1
frml a2t  = mrk - p2
frml h11f = c11 + a11^2*a1t{1}^2 + b11^2*h11{1}
frml h12f = c12 + a11*a22*a1t{1}*a2t{1} + b11*b22*h12{1}
frml h22f = c22 + a22^2*a2t{1}^2 + b22^2*h22{1}
frml logl = h11(t)=h11f(t), h12(t)=h12f(t), h22(t)=h22f(t), $
            rho = h12(t)/sqrt(h11(t)*h22(t)), $
            %logdensity(|| h11(t),h12(t) | h12(t),h22(t) ||, || a1t(t) | a2t(t) ||)
compute p1=0.013, p2=0.012, c11=0.02, c12=0.007, c22=0.006, a11=0.21, a22=0.18, b11=0.9, b22=0.9
maximize(pmethod=simplex,piters=20,iters=200) logl 2 *

* Constant Correlation Model (2-dim) --------------------------------------------------------------------------
end(reset)
OPEN DATA "C:\Users\Jimmy\Desktop\d-hkjp0608.txt"
DATA(FORMAT=PRN,NOLABELS,ORG=COLUMNS,TOP=2,LEFT=4) 1 714 HK JP

log hk
log jp
diff hk
diff jp
set(first=%na) hk = 100*hk
set(first=%na) jp = 100*jp

garch(p=1,q=1,mv=cc) / hk jp

nonlin(parmset=meanparms) p1 p2
frml a1f = hk - p1
frml a2f = jp - p2
compute p1=0.1, p2=0.001
nlsystem(parmset=meanparms,cvout=sigma,noprint) / a1f a2f
set a1t = a1f
set a2t = a2f
set h1 = sigma(1,1)
set h2 = sigma(2,2)
cmom(corr)
# a1t a2t 
compute rho = %cmom(1,2)
set h12 = rho*sqrt(h1*h2)
nonlin(parmset=garchparms) c1 c2 a1 a2 b1 b2 
frml h1f  = c1 + a1*a1t{1}^2 + b1*h1{1}
frml h2f  = c2 + a2*a2t{1}^2 + b2*h2{1}
frml logl = $
	    a1t(t)=a1f(t), a2t(t)=a2f(t), $
	    h1(t)=h1f(t), h2(t)=h2f(t), h12(t)=rho*sqrt(h1(t)*h2(t)), $
	    %logdensity(|| h1(t),h12(t) | h12(t),h2(t) ||, || a1t(t)|a2t(t) ||)
compute c1=0.08,c2=0.05,a1=0.14,a2=0.1,b1=0.8,b2=0.8
maximize(parmset=meanparms+garchparms,iters=200) logl 3 *
? "rho=" rho

* Constant Correlation Model (3 or more dim) --------------------------------------------------------------------------
end(reset)
OPEN DATA "C:\Users\Jimmy\Desktop\d-spcscointc.txt"
DATA(FORMAT=PRN,NOLABELS,ORG=COLUMNS,TOP=2) 1 2275 sp500 csco intc
*
compute n=3
dec vect[series] y(n) u(n)
dec vect[frml] resid(n)
do i=1,3
   set y(i) = ([series]i)
end do i
*
dec vect c(n)
nonlin(parmset=meanparms) c
do i=1,3
   stats(noprint) y(i)
   com c(i) = %mean
end do i
do i=1,n
   frml resid(i) = (y(&i)-c(&i))
end do i
nlsystem(parmset=meanparms,resids=u,noprint) / resid
compute rr=%sigma
dec series[symm] h uu
dec symm hx(n,n)
dec vect ux(n)
gset h = rr
gset uu = rr
*
dec symm qc(n-1,n-1)
dec vect vcv(n) vav(n) vbv(n)
*
function hfcccgarch time
type symm hfcccgarch
type integer time
do i=1,n
   compute hx(i,i)=vcv(i)+vbv(i)*h(time-1)(i,i)+vav(i)*uu(time-1)(i,i)
   do j=1,i-1
      compute hx(i,j)=qc(i-1,j)*sqrt(hx(j,j)*hx(i,i))
   end do j
end do i
compute hfcccgarch=hx
end
*
nonlin(parmset=garchparms) vcv vav vbv qc
declare frml[symm] hf
frml hf = hfcccgarch(t)
frml logl = $
 	    hx = hf(t) , $
   	    %do(i,1,n,u(i)=resid(i)) , $
 	    ux = %xt(u,t), $
 	    h(t)=hx, uu(t)=%outerxx(ux), $
 	    %logdensity(hx,ux)
compute vcv=%xdiag(rr), vav=%const(0.03), vbv=%const(0.8), qc=%const(0.4)
maximize(parmset=meanparms+garchparms,pmethod=simplex,piters=10,iters=200) logl 2 *
* Compare
garch(p=1,q=1,mv=cc) / sp500 csco intc

* DCC --------------------------------------------------------------------------------------------------------------
end(reset)
OPEN DATA "C:\Users\Jimmy\Desktop\m-ibmspln.dat"
DATA(FORMAT=PRN,NOLABELS,ORG=COLUMNS,TOP=2) 1 888 ibm sp500

nonlin(parmset=meanparms) p10 p11 p12 p20
linreg(noprint) ibm
# constant ibm{1} sp500{2}
compute p10=%beta(1), p11=%beta(2), p12=%beta(3)
stats(noprint) sp500
compute p20=%mean

frml u1f = ibm-p10-p11*ibm{1}-p12*sp500{2}
frml u2f = sp500-p20
nlsystem(parmset=meanparms,resids=u,noprint) / u1f u2f
set u1t = u(1)
set u2t = u(2)
set h1t = %sigma(1,1)
set h2t = %sigma(2,2)
set h12t = %sigma(1,2)
* time-varying correlation
set s12t = %sigma(1,2)/sqrt(%sigma(1,1)*%sigma(2,2))

* unconditional correlation
garch(noprint,regressors,p=1,q=1,hseries=gh1,resids=ga1) / ibm
# constant ibm{1} sp500{2}
set epis1 = ga1/sqrt(gh1)
garch(noprint,p=1,q=1,hseries=gh2,resids=ga2) / sp500
set epis2 = ga2/sqrt(gh2)
sstats(mean) / epis1*epis2>>rho
*
nonlin(parmset=garchparms) c1 c2 a1 a2 b1 b2 alpha beta
frml h1f  = c1 + a1*u1t{1}^2 + b1*h1t{1}
frml h2f  = c2 + a2*u2t{1}^2 + b2*h2t{1}
frml q12f = (1-alpha-beta)*rho + alpha*u1t{1}*u2t{1}/sqrt(h1t{1}*h2t{1}) + beta*q12f{1}
frml q11f = (1-alpha-beta) + alpha*u1t{1}^2/h1t{1} + beta*q11f{1}
frml q22f = (1-alpha-beta) + alpha*u2t{1}^2/h2t{1} + beta*q22f{1}
frml s12f = q12f/sqrt(q11f*q22f)
frml logl = u1t=u1f, u2t=u2f, h1t=h1f, h2t=h2f, s12t=s12f , h12t=s12t*sqrt(h1t*h2t), $
			%logdensity(|| h1t(t),h12t(t)|h12t(t),h2t(t) ||,||u1t(t)|u2t(t)||)
compute c1=0.1, c2=0.1, a1=0.1, a2=0.1, b1=0.8, b2=0.8, alpha=0.1, beta=0.8
maximize(parmset=meanparms+garchparms,pmethod=simplex,piters=20,iters=200) logl 5 *

* DCC --------------------------------------------------------------------------------------------------------------
end(reset)
OPEN DATA "C:\Users\Jimmy\Desktop\m-ibmspln.dat"
DATA(FORMAT=PRN,NOLABELS,ORG=COLUMNS,TOP=2) 1 888 ibm sp500
*
nonlin(parmset=meanparms) p10 p11 p12 p20
linreg(noprint) ibm
# constant ibm{1} sp500{2}
compute p10=%beta(1), p11=%beta(2), p12=%beta(3)
stats(noprint) sp500
compute p20=%mean
*
compute n=2
dec vect[series] u(n)
dec vect[frml] resid(n)
dec vect[frml] hd(n)
dec frml[symm] hf qf
dec series[symm] uu h
dec symm hx(n,n)
dec vect ux(n)

frml resid(1) = ibm-p10-p11*ibm{1}-p12*sp500{2}
frml resid(2) = sp500-p20
nlsystem(parmset=meanparms,resids=u,noprint) / resid
gset uu = %sigma
gset h  = %sigma
* time-varying correlation
set s12t = %sigma(1,2)/sqrt(%sigma(1,1)*%sigma(2,2))

* unconditional correlation
garch(noprint,regressors,p=1,q=1,hseries=gh1,resids=ga1) / ibm
# constant ibm{1} sp500{2}
set epis1 = ga1/sqrt(gh1)
garch(noprint,p=1,q=1,hseries=gh2,resids=ga2) / sp500
set epis2 = ga2/sqrt(gh2)
sstats(mean) / epis1*epis2>>rho
compute [rect] qbar = || 1, rho| rho, 1 ||
*
nonlin(parmset=garchparms) c1 c2 a1 a2 b1 b2 alpha beta
frml hd(1) = c1 + a1*uu{1}(1,1) + b1*h{1}(1,1)
frml hd(2) = c2 + a2*uu{1}(2,2) + b2*h{1}(2,2)
frml qf    = sig1=1/sqrt(h{1}(1,1)), $
	     sig2=1/sqrt(h{1}(2,2)), $
	     D=%diag(||sig1,sig2||), $
	     (1-alpha-beta)*qbar + alpha*D*uu{1}*D + beta*qf{1}
frml s12f  = q=qf, q(1,2)/sqrt(q(1,1)*q(2,2))
frml hf    = hx(1,1)=hd(1),hx(2,2)=hd(2),hx(1,2)=s12f*sqrt(hx(1,1)*hx(2,2)),hx
frml logl  = $
             hx = hf(t) , $
             %do(i,1,n,u(i)=resid(i)) , $
             ux = %xt(u,t), $
             h(t)=hx, uu(t)=%outerxx(ux), $
             %logdensity(hx,ux)
compute c1=0.1, c2=0.1, a1=0.1, a2=0.1, b1=0.8, b2=0.8, alpha=0.1, beta=0.8
maximize(parmset=meanparms+garchparms,pmethod=simplex,piters=20,iters=200) logl 5 *
