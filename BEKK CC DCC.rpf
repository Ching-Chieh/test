* Diagonal BEKK -----------------------------------------------------------------------------
end(reset)
OPEN DATA "C:\Users\Jimmy\Desktop\m-pfemrk6508.txt"
DATA(FORMAT=PRN,NOLABELS,ORG=COLUMNS,TOP=2,LEFT=2) 1 528 pfe mrk
garch(p=1,q=1,mv=dbekk,hmatrices=hh,rvector=rr,piters=20,pmethod=simplex,iters=200) / pfe mrk
* Diagonal BEKK -----------------------------------------------------------------------------
end(reset)
OPEN DATA "C:\Users\Jimmy\Desktop\m-pfemrk6508.txt"
DATA(FORMAT=PRN,NOLABELS,ORG=COLUMNS,TOP=2,LEFT=2) 1 528 pfe mrk
VCV(noprint)
# pfe mrk
set h11 = %sigma(1,1)
set h12 = %sigma(1,2)
set h22 = %sigma(2,2)
nonlin p1 p2 c11 c12 c22 a11 a22 b11 b22
frml a1t  = pfe - p1
frml a2t  = mrk - p2
frml h11f = c11 + a11^2*a1t{1}^2 + b11^2*h11{1}
frml h12f = c12 + a11*a22*a1t{1}*a2t{1} + b11*b22*h12{1}
frml h22f = c22 + a22^2*a2t{1}^2 + b22^2*h22{1}
frml logl = h11(t)=h11f(t), h12(t)=h12f(t), h22(t)=h22f(t), $
            %logdensity(|| h11(t),h12(t) | h12(t),h22(t) ||, || a1t(t) | a2t(t) ||)
compute p1=0.013, p2=0.012, c11=0.0004, c12=0.0002, c22=0.0001, a11=0.21, a22= 0.18, b11=b22=0.9
maximize(pmethod=simplex,piters=20,iters=200) logl 2 *

* Constant Correlation Model (2-dim) --------------------------------------------------------------------------
end(reset)
OPEN DATA "C:\Users\Jimmy\Desktop\d-hkjp0608.txt"
DATA(FORMAT=PRN,NOLABELS,ORG=COLUMNS,TOP=2,LEFT=4) 1 714 HK JP

log hk
log jp
diff hk
diff jp
set(first=%na) hk = 100*hk
set(first=%na) jp = 100*jp

garch(p=1,q=1,mv=cc) / hk jp

nonlin(parmset=meanparms) p1 p2
stats(noprint) hk
compute p1=%mean
stats(noprint) jp
compute p2=%mean
frml a1f = hk - p1
frml a2f = jp - p2
nlsystem(parmset=meanparms,noprint) / a1f a2f
set a1t = a1f
set a2t = a2f
set h1 = %sigma(1,1)
set h2 = %sigma(2,2)
set h12 = %sigma(1,2)
compute c1=%sigma(1,1), c2=%sigma(2,2)
cmom(corr)
# a1t a2t 
compute rho = %cmom(1,2)

nonlin(parmset=garchparms) c1 c2 a1 a2 b1 b2 rho
frml h1f  = c1 + a1*a1t{1}^2 + b1*h1{1}
frml h2f  = c2 + a2*a2t{1}^2 + b2*h2{1}
frml logl = $
	    a1t(t)=a1f(t), a2t(t)=a2f(t), $
	    h1(t)=h1f(t), h2(t)=h2f(t), h12(t)=rho*sqrt(h1(t)*h2(t)), $
	    %logdensity(|| h1(t),h12(t) | h12(t),h2(t) ||, || a1t(t)|a2t(t) ||)
compute a1=a2=0.1, b1=b2=0.8
maximize(parmset=meanparms+garchparms,iters=200) logl 3 *

* Constant Correlation Model (3 or more dim) --------------------------------------------------------------------------
end(reset)
OPEN DATA "C:\Users\Jimmy\Desktop\d-spcscointc.txt"
DATA(FORMAT=PRN,NOLABELS,ORG=COLUMNS,TOP=2) 1 2275 sp500 csco intc
*
compute n=3
dec vect[series] y(n) u(n)
dec vect[frml] resid(n)
do i=1,3
   set y(i) = ([series]i)
end do i
*
dec vect c(n)
nonlin(parmset=meanparms) c
do i=1,3
   stats(noprint) y(i)
   com c(i) = %mean
end do i
do i=1,n
   frml resid(i) = (y(&i)-c(&i))
end do i
nlsystem(parmset=meanparms,resids=u,noprint) / resid
compute rr=%sigma
dec series[symm] h uu
dec symm hx(n,n)
dec vect ux(n)
gset h = rr
gset uu = rr
*
dec symm qc(n-1,n-1)
dec vect vcv(n) vav(n) vbv(n)
*
function hfcccgarch time
type symm hfcccgarch
type integer time
do i=1,n
   compute hx(i,i)=vcv(i)+vbv(i)*h(time-1)(i,i)+vav(i)*uu(time-1)(i,i)
   do j=1,i-1
      compute hx(i,j)=qc(i-1,j)*sqrt(hx(j,j)*hx(i,i))
   end do j
end do i
compute hfcccgarch=hx
end
*
nonlin(parmset=garchparms) vcv vav vbv qc
declare frml[symm] hf
frml hf = hfcccgarch(t)
frml logl = $
 	    hx = hf(t) , $
   	    %do(i,1,n,u(i)=resid(i)) , $
 	    ux = %xt(u,t), $
 	    h(t)=hx, uu(t)=%outerxx(ux), $
 	    %logdensity(hx,ux)
compute vcv=%xdiag(rr), vav=%const(0.03), vbv=%const(0.8), qc=%const(0.4)
maximize(parmset=meanparms+garchparms,pmethod=simplex,piters=10,iters=200) logl 2 *
* Compare
garch(p=1,q=1,mv=cc) / sp500 csco intc


